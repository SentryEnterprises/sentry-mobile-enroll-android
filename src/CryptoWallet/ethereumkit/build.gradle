plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}

android {
    namespace 'io.horizontalsystems.ethereumkit'

    compileSdk project.compileSdk

    defaultConfig {
        minSdkVersion project.minSdk
        targetSdkVersion project.targetSdk

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
                arg("room.incremental", true)
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'org.bouncycastle:bcpkix-jdk15on:1.65'

    implementation 'com.github.horizontalsystems:hd-wallet-kit-android:66c2544'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    def scarlet_version = "0.1.12"
    implementation "com.tinder.scarlet:scarlet:$scarlet_version"
    implementation "com.tinder.scarlet:websocket-okhttp:$scarlet_version"
    implementation "com.tinder.scarlet:stream-adapter-rxjava2:$scarlet_version"
    implementation "com.tinder.scarlet:message-adapter-gson:$scarlet_version"
    implementation "com.tinder.scarlet:lifecycle-android:$scarlet_version"

    // Room
    implementation "androidx.room:room-runtime:${project.room_version}"
    implementation "androidx.room:room-rxjava2:${project.room_version}"
    kapt "androidx.room:room-compiler:${project.room_version}"

    //Eip712
    implementation 'org.web3j:crypto:4.9.4'
    api 'org.web3j:abi:4.9.8'

    // Test helpers
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.7'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.7'

    // Spek
    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.9'
    testRuntimeOnly 'org.spekframework.spek2:spek-runner-junit5:2.0.9'
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // Android Instrumentation Test
    implementation 'androidx.annotation:annotation:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.19.1'
    androidTestImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    mavenCentral()
}
